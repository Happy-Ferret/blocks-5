; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=Blocks 5
AppVerName=Blocks 5
AppPublisher=Scherfgen-Software
AppPublisherURL=http://www.scherfgen-software.net/blocks5/
AppSupportURL=http://www.scherfgen-software.net/blocks5/
AppUpdatesURL=http://www.scherfgen-software.net/blocks5/
DefaultDirName={pf}\Blocks 5
DefaultGroupName=Blocks 5
AllowNoIcons=yes
OutputBaseFilename=blocks5-1.1.1
SetupIconFile=setupicon.ico
Compression=lzma2/ultra64
SolidCompression=yes
UninstallFilesDir={app}\uninst

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "german"; MessagesFile: "compiler:Languages\German.isl"

[CustomMessages]
english.InstallingVC2008Runtime=Installing Visual C++ Runtime Libraries ...
english.InstallingOpenAL11=Installing OpenAL ...
english.Configuring=Configuring Blocks 5 ...
english.ShowReadme=Show readme file
english.ShowUserDirectory=Show user directory (screenshots, videos, levels)
english.WindowedMode=Blocks 5 (windowed mode)
english.HQ2XMode=Blocks 5 (HQ2X mode - only for very fast PCs)
english.VisitBlocks5Website=Visit Blocks 5 Website
english.UninstallBlocks5=Uninstall Blocks 5
english.InstallVC2008Runtime=Install Visual C++ 2008 Runtime (game may not work without it)
english.InstallOpenAL11=Install OpenAL 1.1 (game may not work without it)
english.EnableUpdateChecker=Automatically check for game updates
english.ConfigID=en
german.InstallingVC2008Runtime=Installiere Visual C++ 2008-Laufzeitbibliotheken ...
german.InstallingOpenAL11=Installiere OpenAL ...
german.Configuring=Konfiguriere Blocks 5 ...
german.ShowReadme=Readme-Datei anzeigen (englisch)
german.ShowUserDirectory=Benutzerverzeichnis anzeigen (Screenshots, Videos, Levels)
german.WindowedMode=Blocks 5 (Fenstermodus)
german.HQ2XMode=Blocks 5 (HQ2X-Modus - nur für sehr schnelle PCs)
german.VisitBlocks5Website=Blocks 5-Webseite besuchen
german.UninstallBlocks5=Blocks 5 deinstallieren
german.InstallVC2008Runtime=Visual C++ 2008-Laufzeitbibliotheken installieren (Spiel funktioniert sonst möglicherweise nicht)
german.InstallOpenAL11=OpenAL 1.1 installieren (Spiel funktioniert sonst möglicherweise nicht)
german.EnableUpdateChecker=Automatisch nach Spiel-Updates suchen
german.ConfigID=de

[Tasks]
Name: "InstallVC2008Runtime"; Description: "{cm:InstallVC2008Runtime}"
Name: "InstallOpenAL11"; Description: "{cm:InstallOpenAL11}"
Name: "EnableUpdateChecker"; Description: "{cm:EnableUpdateChecker}"
Name: "CreateDesktopIcon"; Description: "{cm:CreateDesktopIcon}"; Flags: unchecked
Name: "ShowReadme"; Description: "{cm:ShowReadme}"

[Files]
Source: "..\stage\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs

[Icons]
Name: "{group}\Blocks 5"; Filename: "{app}\blocks5.exe"; WorkingDir: "{app}"

Name: "{group}\{cm:ShowReadme}"; Filename: "{app}\readme.txt"
Name: "{group}\{cm:ShowUserDirectory}"; Filename: "{app}\showuserdir.exe"
Name: "{group}\{cm:WindowedMode}"; Filename: "{app}\blocks5.exe"; Parameters: "-windowed"; WorkingDir: "{app}"
Name: "{group}\{cm:HQ2XMode}"; Filename: "{app}\blocks5.exe"; Parameters: "-hq2x"; WorkingDir: "{app}"
Name: "{group}\{cm:VisitBlocks5Website}"; Filename: "{app}\Blocks 5 Website.url"; WorkingDir: "{app}"
Name: "{group}\{cm:UninstallBlocks5}"; Filename: "{uninstallexe}"
Name: "{userdesktop}\Blocks 5"; Filename: "{app}\blocks5.exe"; WorkingDir: "{app}"; Tasks: CreateDesktopIcon

[Run]
Filename: "{app}\vcredist_x86.exe"; Parameters: "/qb"; StatusMsg: "{cm:InstallingVC2008Runtime}"; Tasks: InstallVC2008Runtime
Filename: "{app}\oalinst.exe"; Parameters: "-s"; StatusMsg: "{cm:InstallingOpenAL11}"; Tasks: InstallOpenAL11
Filename: "{app}\makeconfig.bat"; Parameters: "{cm:ConfigID}"; StatusMsg: "{cm:Configuring}"; Flags: runhidden
Filename: "{app}\update_checker_disable.bat"; StatusMsg: "{cm:Configuring}"; Flags: runhidden
Filename: "{app}\update_checker_enable.bat"; StatusMsg: "{cm:Configuring}"; Flags: runhidden; Tasks: EnableUpdateChecker
Filename: "notepad.exe"; Parameters: "{app}\readme.txt"; Flags: nowait; Tasks: ShowReadme
; Filename: "{app}\blocks5.exe"; Description: "{cm:LaunchProgram,Blocks 5}"; Flags: nowait postinstall skipifsilent







